managed implementation in class zbp_054906_movie unique;
strict ( 2 );

define behavior for ZI_054906_MovieTP alias Movie
persistent table zabap_movie_a
lock master
authorization master ( instance )
//etag master <field_name>
{
  create ( authorization : global );
  update;
  delete;
  association _Ratings { create; }

  field ( readonly, numbering : managed ) MovieUuid;
  field ( mandatory : create ) Title, Genre, PublishingYear, RuntimeInMin;
  field ( readonly : update ) Title, Genre, PublishingYear, RuntimeInMin;
  field ( readonly ) CreatedAt, CreatedBy, LastChangedAt, LastChangedBy;

  validation validate_genre on save { field Genre; }

  action ( features : instance ) rate_movie parameter ZA_054906_Rating;

  mapping for zabap_movie_a corresponding
    {
      CreatedAt      = created_at;
      CreatedBy      = created_by;
      ImageUrl       = image_url;
      LastChangedAt  = last_changed_at;
      LastChangedBy  = last_changed_by;
      MovieUuid      = movie_uuid;
      PublishingYear = publishing_year;
      RuntimeInMin   = runtime_in_min;
    }
}

define behavior for ZI_054906_RatingTP alias Rating
persistent table zabap_rating_a
lock dependent by _Movie
authorization dependent by _Movie
//etag master <field_name>
{
  update;
  delete;
  association _Movie;

  field ( readonly, numbering : managed ) RatingUuid;
  field ( readonly ) MovieUuid, Rating, RatingDate, UserName;

  determination determine_user_name on modify { create; }
  determination determine_rating_date on modify { create; }

  mapping for zabap_rating_a corresponding
    {
      MovieUuid  = movie_uuid;
      RatingDate = rating_date;
      RatingUuid = rating_uuid;
      UserName   = user_name;
    }
}